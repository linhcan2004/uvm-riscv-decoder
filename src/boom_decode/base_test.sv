class base_test extends uvm_test;
    `uvm_component_utils(base_test)

    function new(string name = "base_test", uvm_component parent = null);
        super.new(name, parent);
    endfunction

    env             e0;
    gen_item_seq    seq;
    virtual decode_if vif;

    virtual function void build_phase(uvm_phase phase);
        super.build_phase(phase);

        // Create the environment
        e0 = env::type_id::create("e0", this);

        // Get virtual IF handle from top level and pass it to everything
        // in env level
        if (!uvm_config_db#(virtual decode_if)::get(this, "", "decode_if", vif))
            `uvm_fatal("TEST", "Did not get vif")
        uvm_config_db#(virtual decode_if)::set(this, "e0.a0.*", "decode_if", vif);

        // Create sequence (child test re-randomizes with its own constraints)
        seq = gen_item_seq::type_id::create("seq");
        seq.randomize();
    endfunction

    virtual task run_phase(uvm_phase phase);
        phase.raise_objection(this);
        apply_reset();
        seq.start(e0.a0.s0);
        #200;
        phase.drop_objection(this);
    endtask

    virtual task apply_reset();
        vif.rstn <= 0;
        repeat(5) @(posedge vif.clk);
        vif.rstn <= 1;
        repeat(10) @(posedge vif.clk);
    endtask
endclass

